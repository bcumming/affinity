cmake_minimum_required(VERSION 3.21)
project(affinity)

# Generate a json file with compilation commands for each file
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

# C++ settings
enable_language(CXX)
set(CMAKE_CXX_STANDARD 11)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# options
option(AFFINITY_MPI "enable MPI target" ON)
set(AFFINITY_GPU_BACKEND "none" CACHE STRING "GPU backend to use (cuda, rocm, none)")
set_property(CACHE AFFINITY_GPU_BACKEND PROPERTY STRINGS cuda rocm none)

# configure the openmp target
find_package(OpenMP REQUIRED)
add_executable(affinity.omp omp.cpp util.cpp)
target_link_libraries(affinity.omp PRIVATE OpenMP::OpenMP_CXX)
install(TARGETS affinity.omp DESTINATION bin)

# configure the MPI target (optional)
if(AFFINITY_MPI)
    message(STATUS "Affinity MPI support enabled")
    find_package(MPI REQUIRED)
    add_executable(affinity.mpi mpi.cpp util.cpp)
    target_link_libraries(affinity.mpi PRIVATE OpenMP::OpenMP_CXX MPI::MPI_CXX)
    install(TARGETS affinity.mpi DESTINATION bin)
endif()

# configure the GPU target (optional)
if(AFFINITY_GPU_BACKEND STREQUAL "cuda")
    if (NOT AFFINITY_MPI)
        message(FATAL_ERROR "GPU backends require MPI support to be enabled.")
    endif()
    find_package(CUDAToolkit 10 REQUIRED)
    set(CUDA TRUE)
    enable_language(CUDA)
    add_executable(affinity.cuda gpu.cpp cuda.cpp util.cpp)
    target_include_directories(affinity.cuda PRIVATE ${CMAKE_CUDA_TOOLKIT_INCLUDE_DIRECTORIES})
    target_compile_definitions(affinity.cuda PRIVATE AFFINITY_CUDA)
    target_link_libraries(affinity.cuda PRIVATE OpenMP::OpenMP_CXX MPI::MPI_CXX CUDA::cudart)
    install(TARGETS affinity.cuda DESTINATION bin)
elseif(AFFINITY_GPU_BACKEND STREQUAL "rocm")
    if (NOT AFFINITY_MPI)
        message(FATAL_ERROR "GPU backends require MPI support to be enabled.")
    endif()
    if(DEFINED ENV{ROCM_PATH})
        set(ROCM_PATH $ENV{ROCM_PATH})
        set(ROCM TRUE)
    else()
        message(FATAL_ERROR "Unable to configure ROCM target - set the ROCM_PATH environment variable.")
    endif()

    find_package(rocm_smi REQUIRED PATHS "${ROCM_PATH}/lib/cmake/rocm_smi")
    find_package(hip REQUIRED PATHS "${ROCM_PATH}/lib/cmake/hip")
    add_executable(affinity.rocm gpu.cpp rocm.cpp util.cpp)
    target_compile_definitions(affinity.rocm PRIVATE __HIP_PLATFORM_AMD__)
    target_compile_definitions(affinity.rocm PRIVATE AFFINITY_ROCM)
    target_include_directories(affinity.rocm PRIVATE "${ROCM_SMI_INCLUDE_DIR}" "${HIP_INCLUDE_DIR}")
    target_link_libraries(affinity.rocm PRIVATE OpenMP::OpenMP_CXX MPI::MPI_CXX)
    target_link_libraries(affinity.rocm PRIVATE ${ROCM_SMI_LIBRARIES})
    target_link_libraries(affinity.rocm PRIVATE hip::host)
    install(TARGETS affinity.rocm DESTINATION bin)
endif()
